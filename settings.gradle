pluginManagement {
    plugins {
        if (enableModernJavaSyntax.toBoolean() || enableJava17RunTasks.toBoolean()) {
            id 'com.diffplug.spotless' version '6.23.3' apply false
        } else {
            id 'com.diffplug.spotless' version '6.13.0' apply false
        }

        if (useNewerKotlinVersion.toBoolean()) {
            // Kotlin Library is bundled into Forgelin-Continuous
            //id 'org.jetbrains.kotlin.jvm' version '2.0.21' apply false
            id 'org.jetbrains.kotlin.kapt' version '2.0.21' apply false
            id 'com.google.devtools.ksp' version '2.0.21-1.0.27' apply false

        } else {
            id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
            id 'org.jetbrains.kotlin.kapt' version '1.8.0' apply false
            id 'com.google.devtools.ksp' version '1.8.0-1.0.9' apply false
        }
    }
    repositories {
        maven {
            // RetroFuturaGradle
            name 'GTNH Maven'
            url 'https://nexus.gtnewhorizons.com/repository/public/'
            //noinspection GroovyAssignabilityCheck
            mavenContent {
                includeGroup 'com.gtnewhorizons'
                includeGroup 'com.gtnewhorizons.retrofuturagradle'
            }
        }
        gradlePluginPortal()
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id 'com.diffplug.blowdryerSetup' version '1.7.0'
    // Automatic toolchain provisioning
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

blowdryerSetup {
    repoSubfolder 'spotless'
    github 'GregTechCEu/Buildscripts', 'tag', 'v1.0.7'
}

rootProject.name = rootProject.projectDir.getName()
